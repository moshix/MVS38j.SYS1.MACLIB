         MACRO                                                          00020001
&NAME    DAR   &GACB,&MF=I                                              00040001
         LCLA  &LNGTH,&INC,&CNT         DEFINE WORK AREAS               00060001
         LCLC  &NAMGACB,&NAMLIST                                        00080001
         LCLC  &MNAME                                                   00100001
         LCLA  &TOTLNG                                                  00120001
         LCLC  &REGAD                                                   00140001
&CNT     SETA  0                        COUNT IS NUMBER OF GACBS        00160001
&INC     SETA  0                        NUMBER OF FULL WORDS            00180001
.NXTST1  AIF   ('&MF' EQ 'I').TYPESL    TEST FOR TYPE S AND             00200001
         AIF   ('&MF' EQ 'L').TYPESL    TYPE L.                         00220001
         AIF   (N'&MF NE 2).ER1         THEN A TEST IS MADE TO          00240001
         AIF   ('&MF(1)' NE 'E').ER1    CHECK PROPER MF=E FORMAT        00260001
         AIF   ('&MF(2)'(1,1) EQ '(').USEREG                            00280001
&NAME    LA    1,&MF(2)                 LOAD ADDRESS OF LIST            00300001
         AGO   .OVUSRG                  INTO REGISTER 1                 00320001
.USEREG  ANOP                           IF A REGISTER WAS SPECIFIED     00340001
&REGAD   SETC  '&MF(2)'(2,K'&MF(2)-2)   LOAD REGISTER INSTRUCTION       00360001
&NAME    LR    1,&REGAD                 LOAD PARAMETER LIST ADDRESS     00380001
.OVUSRG  AIF   (K'&GACB EQ 0).NOMOR      NO GACB IN MACRO TEST          00400001
.NEXT    AIF   (N'&GACB EQ &CNT).NOMOR    CHECK FOR END OF GACB LIST    00420001
&CNT     SETA  &CNT+1                   IF NOT, INCREMENT BOTH          00440001
&INC     SETA  &INC+4                   COUNT AND INCREMENT             00460001
         AIF   (K'&GACB(&CNT) EQ 0).NEXT    CHECK FOR BLANK ENTRY       00480001
         AIF   ('&GACB(&CNT)'(1,1) EQ '(').REGIN   CHECK FOR REG        00500001
&NAMGACB SETC  '&GACB(&CNT)'            LOAD GACB NAME INTO CONSTANT    00520001
         LA    15,&NAMGACB              LOAD GACB ADDRESS INTO          00540001
         ST    15,&INC.(1)              PARAMETER LIST                  00560001
         AGO   .NEXT                    GO TO NEXT GACB ADDRESS         00580001
.REGIN   AIF   ('&GACB(&CNT)'(K'&GACB(&CNT),1) NE ')').ER4              00600001
&NAMGACB SETC  '&GACB(&CNT)'(2,K'&GACB(&CNT)-2)   LOAD REG              00620001
         ST    &NAMGACB,&INC.(1)        STORE REG INTO PARAMETER LIST   00640001
         AGO   .NEXT                    GO TO NEXT GACB ADDRESS         00660001
.NOMOR   SVC   74                       LINK TO DAR ROUTINE             00680001
         MEXIT                                                          00700001
.TYPESL  AIF   (K'&GACB EQ 0).ER3       IF S OR L CHECK FOR GACBS       00720001
&LNGTH   SETA  N'&GACB+1                                                00740001
.PARFIL  AIF   (N'&MF NE 1).ER1         CHECK MF FORMAT                 00760001
         AIF   ('&MF' EQ 'I').TYPES     TEST FOR S FORMAT               00780001
         AIF   (K'&NAME EQ 0).WRNMES    CHECK FOR NAME FIELD            00800001
&MNAME   SETC  '&NAME'                  LOAD NAME INTO CONSTANT         00820001
.AFWRN   DS    0F                        ALIGN ON FULLWORD BOUNDARY     00840001
.GENPAR  ANOP                           GENERATE PARAMETER LIST         00860001
&MNAME   DC    AL1(0)                                                   00880001
         DC    AL1(0)                   RESERVED                        00900001
         DC    AL2(&LNGTH)              LENGTH OF PARAMETER LIST        00920001
         AIF   (K'&GACB EQ 0).JSTAL1    TEST FOR NO GACB ENTRY          00940001
.LOOP    ANOP                                                           00960001
&CNT     SETA  &CNT+1                   KEEP TRACK OF NUMBER OF GACBS   00980001
         AIF   (K'&GACB(&CNT) EQ 0).SETZER   TEST FOR BLANK ENTRY       01000001
         AIF   ('&GACB(&CNT)'(1,1) EQ '(').SETZER   REGISTER TEST       01020001
&NAMGACB SETC  '&GACB(&CNT)'            LOAD GACB NAME INTO CONSTANT    01040001
.PUTIN   DC    A(&NAMGACB)               GACB ADDRESS                   01060001
         AIF   (N'&GACB NE &CNT).LOOP      TEST FOR END OF GACBS        01080001
         MEXIT                                                          01100001
.SETZER  ANOP                                                           01120001
&NAMGACB SETC  '0'                      SET VALUE TO ZERO               01140001
         AGO   .PUTIN                                                   01160001
.JSTAL1  ANOP                           JUST ONE DC STATEMENT           01180001
         DC    A(0)                     GACB ADDRESS                    01200001
         MEXIT                                                          01220001
.TYPES   ANOP                           S TYPE MACRO                    01240001
&MNAME   SETC  'IHB&SYSNDX'             GET SYSTEM NAME                 01260001
&NAME    LA    1,&MNAME                 LOAD PARAMETER LIST ADDRESS     01280001
&TOTLNG  SETA  (&LNGTH+1)*4             GET LENGTH OF LIST AND BRANCH   01300001
         AIF   (K'&GACB EQ 0).NOSTR     TEST FOR NO STORE INSTRUCTION   01320001
.LST     AIF   (N'&GACB EQ &CNT).NOSTMR  TEST FOR END OF GACB LIST      01340001
&CNT     SETA  &CNT+1                   INCREMENT SUBLIST COUNT         01360001
&INC     SETA  &INC+4                                                   01380001
         AIF   (K'&GACB(&CNT) EQ 0).LST     CHECK FOR BLANK GACB ENTRY  01400001
         AIF   ('&GACB(&CNT)'(1,1) NE '(').LST   CHECK FOR NO REG       01420001
         AIF   ('&GACB(&CNT)'(K'&GACB(&CNT),1) NE ')').ER5  REG CHECK   01440001
&NAMGACB SETC  '&GACB(&CNT)'(2,K'&GACB(&CNT)-2)   GET REG NAME          01460001
         ST    &NAMGACB,&MNAME+&INC     STORE REG INTO PARAMETER LIST   01480001
         AGO   .LST                                                     01500001
.NOSTMR  ANOP                                                           01520001
&CNT     SETA  0                        RESET COUNT                     01540001
&INC     SETA  0                        AND INCREMENT.                  01560001
.NOSTR   SVC   74                       LINK TO DAR ROUTINE             01580001
         CNOP  0,4                      ALIGN ON FULLWORD BOUNDARY      01600001
         B     *+&TOTLNG                BRANCH OVER PARAMETER LIST      01620001
         AGO   .GENPAR                  GO TO GENERATE PARAMETER LIST   01640001
.ER1     IHBERMAC 35,,&MF               ERROR IN MF FORMAT              01660001
         MEXIT                                                          01680001
.ER3     IHBERMAC 178,GACB,0            GACB NOT SPECIFIED              01700001
&LNGTH   SETA  2                                                        01720001
         AGO   .PARFIL                                                  01740001
.ER4     IHBERMAC 147,&GACB(&CNT)        INVALID REGISTER NOTATION      01760001
         AGO   .NEXT                                                    01780001
.ER5     IHBERMAC 147,&GACB(&CNT)        INVALID REGISTER NOTATION      01800001
         AGO   .LST                                                     01820001
.WRNMES  IHBERMAC 180,NAME,ZERO                                         01840001
         AGO   .AFWRN                                                   01860001
         MEND                                                           01880001
