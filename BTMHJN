*%BTMHJN:  MACRO KEYS(DOEND);                                  @OY36208 00070017
*GOTO PLSHJN;                      /* BILINGUAL HEADER                  00100027
         MACRO                                                          00116627
&NAME    BTMHJN &LABEL,&HJN,&NOINFLB                           @OY36208 00141217
.********************************************************************** 00150027
.*                                                                    * 00200027
.*  $MAC(BTMHJN)          PROD(BTAM):                          @OY36208 00280007
.*                                                                    * 00300027
.*  DESCRIPTIVE NAME = MODULE IDENTIFIER MACRO                        * 00350027
.*                                                                    * 00600027
.*  FUNCTION = INVOKE THE BTMHJN MACRO TO IDENTIFY A MODULE    @OY36208 00670017
.*                                                                    * 00700027
.*  NOTES =                                                           * 00702727
.*    MACRO LIST POSITIONAL PARAMETERS:                               * 00705427
.*        1ST - PROVIDES THE BRANCH-TO SYMBOL                         * 00708127
.*                (IF OMITTED NO BRANCH IS GENERATED)                 * 00710827
.*        2ND - IGNORED (HJN IS NOW ALWAYS PROVIDED)                  * 00713527
.*        3RD - SUPPRESSES GENERATION OF AN IDENTIFIER LABEL          * 00716227
.*                                                                    * 00718927
.*    PLS INVOCATION EXAMPLES:                                        * 00721627
.*                                                                    * 00724327
.*        ?BTMHJN(PROCNAME) DOEND;                             @OY36208 00728017
.*                                                                    * 00730427
.*        WHERE:  PROCNAME IS THE PROCEDURE NAME                      * 00738027
.*                DOEND SPECIFIES DO-END PAIR TO BE SUPPLIED          * 00753227
.*                                                                    * 00760827
.*        ?BTMHJN(PROCNAME);                                   @OY36208 00771417
.*                                                                    * 00776027
.*        WHERE:  PROCNAME IS THE PROCEDURE NAME                      * 00783627
.*                NO DO-END PAIR TO BE SUPPLIED                       * 00791227
.*                                                                    * 00800027
.*  EXTERNAL REFERENCES = NONE                                        * 00850027
.*                                                                    * 00900027
.*  CHANGE ACTIVITY =                                                 * 00950027
.*    TOOK MACRO IEDHJN FROM TCAM AND NAMED TO BTMHJN          @OY36208 01007017
.* C002700,011200-012800         REMOVED ALL REFERENCES TO     @OY42916 01014007
.* C013600-013800,016100-016120  TCAM, TCAM APARS, AND         @OY42916 01020997
.* C016160-016200,018000         COMMENT LINES THAT DO NOT     @OY42916 01027987
.* C025700,027510                APPLY TO BTAM.                @OY42916 01034977
.* D004000-005500,010000-010002                                @OY42916 01041967
.*                                                                    * 01050027
.********************************************************************** 01100027
         GBLB  &INFLBL               INFO LABEL WAS GENERATED           01150017
         LCLA  &K                    &K=LENGTH OF SYSPARM               01180007
         LCLC  &HJA,&HJB,&HJC        &HJC=PTF/LEVEL IDENTIFIER          01209997
&HJC     SETC  '       '             DEFAULT PTF/LEVEL (BLANKS)         01239987
&K       SETA  K'&SYSPARM            SYSPARM LENGTH                     01269977
&HJA     SETC  '&SYSPARM'(1,4)                                          01300027
&HJB     SETC  '&SYSPARM'(5,4)                                          01350027
         AIF   (&K LT 15).CODE       NO PTF/LEVEL VALUE PASSED          01370017
&HJC     SETC  '&SYSPARM'(9,7)       SET PTF/LEVEL VALUE                01380007
.CODE    ANOP                                                           01389997
         AIF   ('&LABEL' EQ '').NOBRNCH                                 01400027
         B     &LABEL .              BRANCH AROUND CONSTANTS            01450027
         SPACE 1                                                        01500027
.NOBRNCH ANOP                                                           01550027
         AIF   ('&NAME' EQ '').NOLABEL                                  01600027
         AIF   (&INFLBL OR ('&NOINFLB' NE '')).LABEL                    01611017
&INFLBL  SETB  1                     INDICATE INFO LBL GENERATED        01612007
MODINF   EQU   *                     MODULE ID LABEL                    01614027
.LABEL   ANOP                        GENERATE MODULE IDENT              01626017
.****                         MODULE IDENT NOW 8 CHARACTERS             01636007
         DC    CL8'&NAME' .          MODULE IDENTIFIER                  01650027
.NOLABEL ANOP                                                           01700027
         DC    X'&HJA' .               DATE OF MODIFICATION             01750027
.****                         HJN NOW PROVIDED UNCONDITIONALLY          01820017
         DC    X'&HJB' .               HJN OF MODIFICATION              01850027
.DATE    ANOP                                                           01900027
.****                         PTF/LEVEL INSERTED                        01910027
         DC    C' ',C'&HJC' .       DEVELOPMENT OR PTF LEVEL            01920027
         DS    0H                                                       01930027
         AIF   ('&LABEL' EQ '').NOLBL                                   01950027
         SPACE 1                                                        02000027
&LABEL   DS    0H                                                       02050027
.NOLBL   ANOP                                                           02100027
         MEND                                                           02150027
*                                      END OF ASSEMBLER PORTION      */ 02200027
*PLSHJN:;                                                               02250027
*DCL PARM CHAR;                                                         02300027
* DCL INVOC    CHAR;                /* INVOCATION STRING             */ 02316627
*   INVOC=MACNAME||MACLIST||' '||MACKEYS; /* SET INVOCATION STRING   */ 02333227
*   PARM=MACLIST(1);                                                    02349827
*   IF DOEND^='' THEN               /* IF DO-END PAIR TO BE CODED    */ 02366427
*     ANS('DO;'||COMMENT(INVOC)) COL(5);/* SUPPLY DO STATEMENT      */  02383027
*ANS('GEN NOREFS NOSETS;') COL(2);                                      02400027
*ANS('PARM') COL(1);                                                    02500027
*ANS('BTMHJN AFTERHJN,HJN') COL(10);                                    02580007
*ANS('MODULE IDENTIFICATION') COL(40);                                  02600027
*ANS('@ENDGEN') COL(2);                                                 02650027
*   IF DOEND^='' THEN               /* IF DO-END PAIR TO BE CODED    */ 02675027
*     ANS('END;') COL(5);           /* SUPPLY END STATEMENT          */ 02700027
*ANS('@SPACE;') COL(5);                                                 02725027
*%END BTMHJN;                                                           02752007
